group 'com.asakusafw.example.jdbc'

buildscript {
    repositories {
        maven { url 'http://asakusafw.s3.amazonaws.com/maven/releases' }
        maven { url 'http://asakusafw.s3.amazonaws.com/maven/snapshots' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.asakusafw.gradle', name: 'asakusa-distribution', version: '0.10.3-RC1'
        classpath group: 'com.h2database', name: 'h2', version: '1.4.192'
    }
}

apply plugin: 'asakusafw-sdk'
apply plugin: 'asakusafw-organizer'
apply plugin: 'asakusafw-mapreduce'
apply plugin: 'asakusafw-spark'
apply plugin: 'eclipse'

configurations {
    pgsqlJdbcDriver
    h2JdbcDriver
}

dependencies {
    pgsqlJdbcDriver group: 'postgresql', name: 'postgresql', version: '9.1-901-1.jdbc4'
    h2JdbcDriver group: 'com.h2database', name: 'h2', version: '1.4.192'
}

asakusafwOrganizer {
    profiles.dev {
        assembly.into('') {
            put 'src/dist/dev'
        }
        assembly.into('windgate/plugin') {
            put configurations.h2JdbcDriver
        }
    }
    profiles.prod {
        windgate.retryableEnabled true
        assembly.into('') {
            put 'src/dist/prod'
        }
        assembly.into('windgate/plugin') {
            put configurations.pgsqlJdbcDriver
        }
    }
}

task stopH2 {
    doLast {
        println 'Stop H2...'
        org.h2.tools.Server.main("-tcpShutdown", "tcp://localhost:9092")
    }
}

test {
    doFirst {
        println 'Start H2...'
        delete "${buildDir}/h2"
        org.h2.tools.Server.main("-tcp", "-baseDir", "${buildDir}/h2")
        org.h2.tools.RunScript.execute("jdbc:h2:tcp://localhost:9092/asakusa", "asakusa", "asakusa", "src/main/sql/model_ddl.sql", java.nio.charset.StandardCharsets.UTF_8, false)
    }
    finalizedBy stopH2
}
